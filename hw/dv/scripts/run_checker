#!/bin/csh -f
# Build checker if needed
# Run checker
# Check for errors
# Report results
# Run it from your test dir (i.e. debug/psys/<config_dir>/run/concert*)

set vcs_run = 0
if ( -f vcs.log ) then
    set vcs_run = 1
endif
if ( -d ../../dv ) then
    set vcs_run = 1
endif

if ( $vcs_run != 0 ) then
    if ( ! -f checker.trc ) then
	echo "input file checker.trc not found!"
	exit 1
    endif

    set version = ../../lib64/checker
    if (! -f $version ) then
	echo "Building $version ..."
        pushd ../..
        if (! -f dv/Makefile.checker ) then
            echo "$cwd/dv/Makefile.checker not found!"
            exit 1
        endif
        cp -p dv/Makefile.checker .
        make -f Makefile.checker >&! make.checker.log
        if ( $status != 0 ) then
    	echo "cmd: make -f Makefile.checker >&! make.checker.log FAIL!"
    	exit 1
        endif
        popd
    endif
    if (! -f $version ) then
        echo "checker $version not found!"
        exit 1
    endif

    echo "Running $version ..."
    $version >&! checker.out
    set saved_status = $status

    if ( $saved_status != 0 ) then
        echo "checker run return BAD status $saved_status see checker.out"
        exit 1
    endif
    set err = `grep ERR checker.log`
    if ( $status == 0 ) then
        echo "CHECKER FAIL: in $cwd : see checker.log: $err"
    else
        echo "CHECKER PASS: in $cwd : No errors in checker.log"
    endif
    exit 0    
endif

if ( -f checker.trc ) then
    gzip checker.trc
endif

if (! -f checker.trc.gz ) then
    echo "input file  checker.trc.gz not found!"
    exit 1
endif

setenv CARBON_TOOL_HOME /engr/dev/tools/carbon

source ${CARBON_TOOL_HOME}/script/checker_setup.csh

set local = 0
if ( -f ../../../lib64/checker ) then
    set path = ( $cwd/../../../lib64 $path )
#    echo "RUNNING LOCAL CHECKER"
    set local = 1
#    if ( -f checker.trc.gz ) then
#	gunzip checker.trc.gz
#    endif
#    if ( ! -f checker.trc ) then
#	echo "file $cwd/checker.trc not found!"
#	exit 1
#    endif
endif

set version = `which checker`

if (! -f $version ) then
    echo "Checker executable $version not found!"
    exit 1
endif

if ( $local != 0 ) then
    echo "Running LOCAL CHECKER $version ..."
else    
    echo "Running $version ..."
endif

checker >&! checker.out 
set saved_status = $status

if ( $saved_status != 0 ) then
    echo "checker run return BAD status $saved_status see checker.out"
    exit 1
endif
set err = `zgrep ERR checker.log.gz`
if ( $status == 0 ) then
    echo "CHECKER FAIL: see $cwd/checker.log.gz : $err"
else
    echo "CHECKER PASS: No errors in checker.log.gz"
endif

if ( -f checker.trc ) then
    gzip checker.trc
endif

exit 0    
